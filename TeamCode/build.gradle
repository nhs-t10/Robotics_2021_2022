//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here
plugins {
    id "com.github.node-gradle.node" version "3.0.1"
    id "org.sonarqube" version "3.3"
}

apply plugin: 'jacoco'

// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

dependencies {
    implementation project(':FtcRobotController')
    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')
    implementation 'junit:junit:4.13.2'
    testImplementation 'junit:junit:4.13.2'
    implementation 'org.jetbrains:annotations-java5:23.0.0'
}

task cleanupOldGeneratedFiles(type: NodeTask) {
    script = file("src/main/scripts/cleanup/index.js")
}

task generateAutoautoSources(type: NodeTask) {
    script = file("src/main/scripts/autoauto-compiler/index.js")
}


task recordBuildHistory(type: NodeTask) {
    script = file("src/main/scripts/buildhistory/index.js")
}

sonarqube {
    properties {
        property "sonar.projectKey", "nhs-t10_Robotics_2021_2022"
        property "sonar.organization", "nhs-t10"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions", "**/unitTests/**"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/coverage/test/debug/report.xml"
    }
}

android {
    androidResources {
        noCompress 'tflite'
    }
    gradle.projectsEvaluated {
        preBuild.dependsOn(cleanupOldGeneratedFiles)
        preBuild.dependsOn(generateAutoautoSources)
        preBuild.dependsOn(recordBuildHistory)
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
    sourceSets {
        debug {
            java.srcDir("gen/")
        }
        main {
            java.srcDir("gen/")

        }
    }
    jacoco {
        version = '0.7.9'
    }
    testOptions {
        unitTests.all {
            reports {
                junitXml.enabled = true
            }
        }
    }
    defaultConfig {
        minSdkVersion 26
    }
}

node {
    download = true
    version = "12.18.3"
    distBaseUrl = "https://nodejs.org/dist"
}