// THIS CODE MIGHT HAVE AN ERROR. THIS IS BECAUSE WE USED driveOmni AS IF IT IS driveOmni(v,h,r)
// AS OF 1/20/2022 driveOmni WORKED AS IF IT IS driveOmni(v,r,-h)

#init:
    goto sampling


//uses CV, saves CV reading to `value`
#sampling:
    let value = getCVPositionNumberWhereZeroIsLeftOneIsMiddleAndTwoIsRight(), log(value), after 1s next;
    goto clawout

//The robot starts folded up so that it's inside an 18x18 cube. `clawout` moves it off the wall then folds out the arms to their full extent.
#clawout:
    driveOmni(0.5,0,0),after 4cm next;
    setServoPosition("intakeMoverLeft", -1.0), setServoPosition("intakeMoverRight", 1.0), next;
    setServoPower("nateMoverLeft", -0.5), setServoPower("nateMoverRight", 0.5), after 3800ms next;
    setServoPower("nateMoverLeft", 0.0), setServoPower("nateMoverRight", 0.0), next;
    goto duckSpin



//do a 180, drive left, spin carousel for 3sec, drive right to be near the depot, do a 180
#duckSpin:
    driveOmni(0, 0.7, 0), after 175degs next;
    driveOmni(0, 0, 0.7),after 20cm next;
    driveOmni(0.1, 0, 0.2), after 500ms setMotorPower("Carousel", 0.4), after 4.5cm next, after 3000ms next;
    setMotorPower("Carousel", 0), next;
    driveOmni(0,0, -1), after 40cm next;
    driveOmni(0,0.7,0), after 160degs next;
    driveOmni(0,0,0), goto split

//based on the CV reading, chooses the correct state path to go to (adjusted for blue)
#split:
    if(value == 0) goto thirdPosition, if(value == 1) goto secondPosition, if(value == 2) goto firstPosition

//moves claw to position one
#firstPosition:
    positionOne(),  when (hasEncodedMovement("ClawMotor") == false) { log("movement done"), next };
    goto towerDropOff

//moves claw to position two
#secondPosition:
    positionTwo(),  when (hasEncodedMovement("ClawMotor") == false) { log("movement done"), next };
    goto towerDropOff

//moves claw to position three
#thirdPosition:
    positionThree(),  when (hasEncodedMovement("ClawMotor") == false) { log("movement done"), next };
    goto towerDropOff

//moves the robot forwards so the claw is where it should be to drop off the freight
#towerDropOff:
    driveOmni(0.7,0,0), after 5cm next;
    setClawOpen(true), after 500ms goto finalMovement;

//moves the robot away from the shipping hub, closes the claw, turns right, moves forward, and parks in the depot
#finalMovement:
    driveOmni(-0.7,0,0), after 3cm next;
    setClawOpen(false), positionNeutral(), when (hasEncodedMovement("ClawMotor") == false) next, after 2000ms next;
    driveOmni(0,0.7,0), after 83degs next;
    driveOmni(0.7,0,0), after 31cm next;
    driveOmni(0,0,0.7), after 2cm next;
    goto parking

//brings the robot to a complete stop
#parking:
    driveRaw(0,0,0,0)